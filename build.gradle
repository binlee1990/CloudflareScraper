import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '4.0.3'
}

group = 'xyz.hexile'
version = '0.1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    implementation 'commons-io:commons-io:2.6'
    implementation 'org.jsoup:jsoup:1.11.3'
}

task sourcesForRelease(type: Copy) {
    from ('src/main/java') {
        //include '**/CloudflareScraperInfo.java'
        filter(ReplaceTokens, tokens: [
                version: version,
                author:  "Giacomo Ferretti",
                website: "https://hexile.xyz/",
                github:  "https://github.com/giacomoferretti/CloudflareScraper"
        ])
    }
    into 'build/filteredSrc'

    includeEmptyDirs = false
}

task generateJavaSources(type: SourceTask) {
    source = sourcesForRelease.destinationDir

    dependsOn sourcesForRelease
}

compileJava {
    source = generateJavaSources.source

    dependsOn generateJavaSources
}

jar {
    manifest {
        attributes(
                'Implementation-Version': version,
                //'Built-By'              : System.properties['user.name'],
                'Build-Timestamp'       : new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Created-By'            : "Gradle ${gradle.gradleVersion}",
                'Build-Jdk'             : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                //'Build-OS'              : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

shadowJar.classifier = 'shaded'

build {
    dependsOn jar
    dependsOn shadowJar
}